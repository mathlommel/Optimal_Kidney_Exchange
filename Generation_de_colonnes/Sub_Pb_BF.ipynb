{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "e1fdfad4",
   "metadata": {},
   "source": [
    "# Sous problème - Bellman-Ford\n",
    "\n",
    "Dans ce notebook, nous définissons la seconde méthode de résolution du sous problème de la génération de colonnes."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e84e2b58",
   "metadata": {},
   "outputs": [],
   "source": [
    "function sub_pb_BF(A,W,p,n,K,preds,eps)\n",
    "    \"\"\"\n",
    "    Résout le sous problème de la génération de colonnes avec l'algorithme de Bellman-Ford\n",
    "\n",
    "    Entrées :\n",
    "        - A     : Array   - Matrice des arcs du graphe\n",
    "        - W     : Array   - Matrice des poids de chaque arc\n",
    "        - p     : Array   - Solution optimale du dual de la relaxation\n",
    "        - n     : Integer - Nombre de sommets dans le graphe\n",
    "        - K     : Integer - Taille maximale des cycles\n",
    "        - preds : Array   - Liste des prédecesseurs de chaque sommet\n",
    "        - eps   : Float   - Tolérance pour la comparaison à 0\n",
    "    \n",
    "    Sorties :   \n",
    "        - cycle      : Array - Cycle de coût positif identifié\n",
    "        - cout_cycle : Float - Coût de ce cycle\n",
    "    \"\"\"\n",
    "    # On démarre de chaque noeud origine possible\n",
    "    for o in 1:(n-1)\n",
    "        #############################################\n",
    "        ### Recherche des meilleurs prédécesseurs ###\n",
    "        #############################################\n",
    "        # On va chercher les chemins de coût max allant aux autres sommets, en au plus K arcs\n",
    "        # Historique des distances / prédécesseurs\n",
    "        Dist = []\n",
    "        Pred = []\n",
    "        \n",
    "        # Distances courantes pour aller de o à chaque sommet\n",
    "        distances = [-Inf for i in 1:n]\n",
    "        distances[o] = 0\n",
    "        push!(Dist,copy(distances))\n",
    "        \n",
    "        # Liste courante des meilleurs prédecesseur de chaque noeud\n",
    "        predecessors = [-1 for i in 1:n]\n",
    "        push!(Pred,copy(predecessors))\n",
    "        \n",
    "        # Pour des chemins de longueur <= K\n",
    "        for k in 1:K  \n",
    "            # On parcourt tous les arcs du graphe\n",
    "            for a in A\n",
    "                # Et on met à jour les distances max entre o et chaque noeud (en mettant à jour leur prédécesseur)\n",
    "                if Dist[k][a[1]]>-Inf && Dist[k][a[1]] + (W[a[1],a[2]] - p[a[2]]) > Dist[k][a[2]]\n",
    "                    distances[a[2]] = Dist[k][a[1]] + (W[a[1],a[2]] - p[a[2]])\n",
    "                    predecessors[a[2]] = a[1]\n",
    "                end\n",
    "            end \n",
    "            # On ajoute les distances / prédécesseurs courants dans les listes historiques\n",
    "            push!(Dist,copy(distances))\n",
    "            push!(Pred,copy(predecessors))\n",
    "        end\n",
    "\n",
    "        #############################################\n",
    "        ###   Meilleur prédécesseur de l'origine  ###\n",
    "        #############################################\n",
    "        # Meilleur prédécesseur de l'origine\n",
    "        best_pred = Pred[K+1][o]\n",
    "        \n",
    "        # Si l'origine a un prédécesseur\n",
    "        if best_pred != -1\n",
    "            #############################################\n",
    "            ###   Identification du meilleur cycle    ###\n",
    "            #############################################\n",
    "            # Initialisation du coût du cycle\n",
    "            cout_cycle = 0\n",
    "            # On ajoute le premier arc du cycle, en stockant le prédécesseur ajouté, et en mettant à jour le coût\n",
    "            cycle = [(best_pred,o)]\n",
    "            pred_cycle = [best_pred]\n",
    "            cout_cycle += W[best_pred,o] - p[o]\n",
    "            \n",
    "            # On remonte les arcs pour construire le cycle\n",
    "            cpt = K\n",
    "            # Sommet d'arrivée du dernier arc ajouté\n",
    "            last = o\n",
    "            # Booléen pour la gestion des sous-cycles\n",
    "            test_subcycle = false\n",
    "            \n",
    "            # Tant qu'un cycle n'est pas obtenu ou qu'un sous-cycle n'a pas été identifié\n",
    "            while best_pred != o && !test_subcycle\n",
    "                # On cherche la dernière mise à jour du prédécesseur de \"last\" (sommet d'arrivée du dernier arc ajouté)\n",
    "                if (Pred[cpt][last] == best_pred)\n",
    "                    cpt -= 1\n",
    "                else \n",
    "                    # Identification des deux sommets de l'arc suivant\n",
    "                    temp = best_pred\n",
    "                    best_pred = Pred[cpt][temp]\n",
    "\n",
    "                    # Si le prédécesseur de l'arc n'a pas encore été visité, on ajoute l'arc\n",
    "                    if !(best_pred in pred_cycle) \n",
    "                        # Ajout de l'arc dans le cycle\n",
    "                        push!(cycle,(best_pred,temp))\n",
    "                        # Mise à jour du coût\n",
    "                        cout_cycle += W[best_pred,temp] - p[temp]\n",
    "                        # Mise à jour de l'historique des prédécesseurs\n",
    "                        push!(pred_cycle,best_pred)\n",
    "                        # On décrémente le compteur et on met à jour \"last\"\n",
    "                        cpt -= 1\n",
    "                        last = temp\n",
    "                    else\n",
    "                        # Sinon, il existe un sous-cycle\n",
    "                        # Ajout du premier arc du sous-cycle\n",
    "                        subcycle = [(best_pred,temp)]\n",
    "                        # Initialisation de son coût\n",
    "                        cout_subcycle = W[best_pred,temp] - p[temp]\n",
    "                        # Sommet \"pivot\", ayant 2 prédécesseurs\n",
    "                        pivot = temp\n",
    "                        # Tant qu'un cycle n'est pas obtenu\n",
    "                        while pivot != best_pred\n",
    "                            # On ajoute le dernier arc du cycle construit jusqu'alors\n",
    "                            arc = pop!(cycle)\n",
    "                            push!(subcycle,arc)\n",
    "                            # Et on met à jour le pivot, ainsi que le coût du sous-cycle\n",
    "                            pivot = arc[2]\n",
    "                            cout_subcycle += W[arc[1],arc[2]] - p[arc[2]]\n",
    "                        end\n",
    "                        # Opération annulant le reverse de sortie\n",
    "                        cycle = reverse(subcycle)\n",
    "                        # Mise à jour du cout du cycle\n",
    "                        cout_cycle = cout_subcycle\n",
    "                        # On actualise la booléen pour sortir de la boucle\n",
    "                        test_subcycle = true\n",
    "                    end\n",
    "                end\n",
    "            end\n",
    "            # On remet le cycle dans le bon ordre \n",
    "            cycle = reverse(cycle)\n",
    "                \n",
    "            #############################################\n",
    "            ##  Décision en fonction du coût du cycle  ##\n",
    "            #############################################\n",
    "            # On renvoie le cycle et son coût si celui-ci est positif\n",
    "            if cout_cycle > eps\n",
    "                return cycle, cout_cycle\n",
    "            end\n",
    "            \n",
    "        end\n",
    "        # Si on arrive ici, cela signifie qu'il n'y a pas de cycle positif passant par o\n",
    "        # --> On supprime les arcs contenant o dans l'itération suivante\n",
    "        A = filter(arc -> !(o in arc), A)\n",
    "    end\n",
    "    # Si on arrive ici, c'est que le cycle de coût max est négatif \n",
    "    # --> on ne renvoie rien\n",
    "    return nothing, 0\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "975d3ff1-fa2f-4f21-a626-d619f9cd5c62",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Résolution par Bellman-Ford importée avec succès.\n"
     ]
    }
   ],
   "source": [
    "println(\"Résolution par Bellman-Ford importée avec succès.\")"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.9.0",
   "language": "julia",
   "name": "julia-1.9"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.9.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
