{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "30350167",
   "metadata": {},
   "source": [
    "# Formulations compactes\n",
    "\n",
    "Dans ce notebook, nous définissons les formulations compactes écrites en 4e année, pour résoudre le problème de transplantation d'organes en domino dans 3 cas différents: \n",
    "\n",
    "- En considérant qu'il n'y a pas de limite de taille sur les cycles solution\n",
    "- En considérant qu'on ne peut réaliser que des associations 2 à 2 (cycles de taille 2)\n",
    "- En considérant une taille maximale K, et en ajoutant des hôpitaux \"virtuels\" dans lesquels les transplantations sont faites"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "010b615f-503e-44c7-8955-428d60a70f73",
   "metadata": {},
   "source": [
    "## Fonction déterminant les cycles solutions\n",
    "\n",
    "La méthode suivante récupère la variable réponse des modèles (1) et (2), et renvoie les cycles solutions de ces deux formulations."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "be608453",
   "metadata": {},
   "outputs": [],
   "source": [
    "function compute_solution(model)\n",
    "    \"\"\"\n",
    "    Crée les cycles solutions rendu par le modèle fourni.\n",
    "    \n",
    "    Entrées : \n",
    "        - model : JuMP Model - Modèle résolu du problème\n",
    "    \n",
    "    Sorties : \n",
    "        - cycles : Array - Cycles solutions\n",
    "    \"\"\"\n",
    "    # Récupération des valeurs optimales des variables et de la valeur optimale\n",
    "    X = value.(model[:x])\n",
    "\n",
    "    # Récupération des arcs solution\n",
    "    arcs = []\n",
    "    for (i,j) in axes(X)[1]\n",
    "        if abs(X[(i,j)]-1) < 1e-6\n",
    "            push!(arcs,(i,j))\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    \n",
    "    # Création de la liste des cycles solution\n",
    "    cycles = []\n",
    "    test = true\n",
    "\n",
    "    cycle = [] \n",
    "    next = arcs[1]\n",
    "    \n",
    "    # Premier affichage\n",
    "    println(\"Cycles : \")\n",
    "\n",
    "    # Tant qu'il reste des arcs à traiter\n",
    "    while test\n",
    "        # On ajoute l'arc au cycle courant\n",
    "        push!(cycle,next)\n",
    "\n",
    "        # Et on le supprime de la liste des arcs à traiter\n",
    "        arcs = filter(t -> t != next,arcs)\n",
    "\n",
    "        # On met à jour l'arc suivant\n",
    "        next = filter(t -> next[2] in t, arcs)\n",
    "        if length(next) > 0\n",
    "            # Cas où le cycle courant n'est pas terminé\n",
    "            next = next[1]\n",
    "        else\n",
    "            # Cas où le cycle courant est terminé\n",
    "            ## On l'ajoute au vecteur des cycles solution\n",
    "            push!(cycles,cycle)\n",
    "            print(\"  - \")\n",
    "            println(cycle)\n",
    "\n",
    "            if length(arcs) > 0\n",
    "                # S'il reste des arcs à parcourir, on recommence\n",
    "                next = arcs[1]\n",
    "                cycle = []\n",
    "            else \n",
    "                # Sinon, on sort de la boucle\n",
    "                test = false\n",
    "            end\n",
    "        end\n",
    "    end \n",
    "    return cycles\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "943af9cc-4547-497b-ad40-00a0676a637c",
   "metadata": {},
   "source": [
    "## Formulation sans limite de taille"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "29aae059",
   "metadata": {},
   "outputs": [],
   "source": [
    "function compact_cycle_infini(graph,logs)\n",
    "    \"\"\"\n",
    "    Crée le modèle linéaire associé au problème de transplantation en domino, sans contrainte de longueur.\n",
    "\n",
    "    Entrées : \n",
    "        - graph : Array   - Vecteur contenant les propriétés du graphe (n,A,W,preds,succ)\n",
    "        - logs  : Boolean - Booléen pour l'affichage de la solution\n",
    "\n",
    "    Sortie : \n",
    "        - objective_value(model) : Float      - Valeur optimale du modèle résolu \n",
    "        - model                  : JuMP Model - Modèle du problème\n",
    "        - timer                  : Float      - Temps de calcul\n",
    "    \"\"\"\n",
    "    # Récupération du graphe d'étude\n",
    "    n = graph[1]\n",
    "    A = graph[2]\n",
    "    W = graph[3]\n",
    "    preds = graph[4]\n",
    "    succ = graph[5]\n",
    "    \n",
    "    # Création du vecteur des sommets\n",
    "    V=Array(1:n)\n",
    "    \n",
    "    # Création du modèle JuMP\n",
    "    model = Model(HiGHS.Optimizer); \n",
    "    set_optimizer_attribute(model,\"output_flag\",false)\n",
    "    \n",
    "    # Variables\n",
    "    ## pour tout arc (i,j), x[(i,j)] = 1 si l'arc (i,j) est choisi dans la solution, 0 sinon\n",
    "    @variable(model, x[A], Bin);\n",
    "    \n",
    "    # Fonction Objectif: On souhaite maximiser le coût des transplantations\n",
    "    @objective(model, Max, sum(x[a]*W[a[1],a[2]] for a in A));\n",
    "    \n",
    "    # Contraintes\n",
    "    ## Un couple peut donner, au plus, à un seul autre couple\n",
    "    @constraint(model,c1[i in V],sum(x[(i,j)] for j in succ[i])<=1)\n",
    "    \n",
    "    ## Si un couple donne à un autre, alors il doit recevoir\n",
    "    @constraint(model,c2[i in V],sum(x[(i,j)] for j in succ[i])==sum(x[(j,i)] for j in preds[i]))\n",
    "    \n",
    "    # Résolution du modèle\n",
    "    timer = @timed optimize!(model);  \n",
    "    \n",
    "    # Affichage de la solution si souhaité\n",
    "    if logs\n",
    "        println(\"***** Taille Infinie *****\\n\");\n",
    "        # Statistiques de la solution\n",
    "        println(\"Statut de la solution = \", termination_status(model));\n",
    "        println(\"Valeur optimale = \", objective_value(model));\n",
    "        println(\"Temps = \", timer[2])\n",
    "    end\n",
    "    return objective_value(model),model,timer;\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "427a5874-6296-4c2c-81fb-83e4d8341ba6",
   "metadata": {},
   "source": [
    "## Formulation avec cycles de taille $\\le$ 2"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "1fe48222",
   "metadata": {},
   "outputs": [],
   "source": [
    "function compact_cycle_2(graph,logs)\n",
    "    \"\"\"\n",
    "    Crée le modèle de transplantation avec cycles de taille 2\n",
    "\n",
    "    Entrées : \n",
    "        - graph : Array   - Vecteur contenant les propriétés du graphe (n,A,W)\n",
    "        - logs  : Boolean - Booléen pour l'affichage de la solution\n",
    "\n",
    "    Sortie : \n",
    "        - objective_value(model) : Float      - Valeur optimale du modèle résolu \n",
    "        - model                  : JuMP Model - Modèle du problème\n",
    "        - timer                  : Float      - Temps de calcul\n",
    "    \"\"\"\n",
    "    # Récupération des propriétés du graphe\n",
    "    n = graph[1]\n",
    "    A = graph[2]\n",
    "    W = graph[3]\n",
    "    \n",
    "    # Vecteur des noeuds\n",
    "    V=Array(1:n)\n",
    "    \n",
    "    # On crée les listes A_2 et V_2\n",
    "    A_2=[]\n",
    "    V_2=[]\n",
    "    for a in A\n",
    "        b=(a[2],a[1])\n",
    "        if b in A \n",
    "            push!(A_2,b)\n",
    "            if !(a[1] in V_2)\n",
    "                push!(V_2,a[1])\n",
    "            end\n",
    "            if !(a[2] in V_2)\n",
    "                push!(V_2,a[2])\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    # Liste des successeurs de chaque sommet de vertices\n",
    "    succ=[]\n",
    "    for i in V\n",
    "        push!(succ,[])\n",
    "    end \n",
    "    \n",
    "    for a in A_2\n",
    "        push!(succ[a[1]],a[2])\n",
    "    end\n",
    "    \n",
    "    # Création du modèle\n",
    "    model = Model(HiGHS.Optimizer); \n",
    "    set_optimizer_attribute(model,\"output_flag\",false)\n",
    "    \n",
    "    # Variables\n",
    "    ## pour tout arc (i,j) de A_2, x[i,j]=1 si l'arc est choisi dans la solution, 0 sinon\n",
    "    @variable(model, x[A_2], Bin);\n",
    "    \n",
    "    # Fonction objectif: On souhaite maximiser la somme des coûts des transplantations\n",
    "    @objective(model, Max, sum(x[a]*W[a[1],a[2]] for a in A_2));\n",
    "    \n",
    "    # Contraintes \n",
    "    ## Un couple peut donner, au plus, 1 fois\n",
    "    @constraint(model,c1[i in V_2],sum(x[(i,j)] for j in succ[i])<=1)\n",
    "    ## Si un couple donne à un autre couple, alors il doit aussi recevoir\n",
    "    @constraint(model,c2[(i,j) in A_2],x[(i,j)]==x[(j,i)])\n",
    "    \n",
    "    # Résolution du modèle\n",
    "    timer = @timed optimize!(model);  \n",
    "    \n",
    "    # Affichage de la solution si souhaité\n",
    "    if logs\n",
    "        println(\"***** Taille 2 *****\");\n",
    "        # Statistiques de la solution\n",
    "        println(\"Statut de la solution = \", termination_status(model));\n",
    "        println(\"Valeur optimale = \", objective_value(model));\n",
    "        println(\"Temps = \", timer[2])\n",
    "    end\n",
    "    \n",
    "    return objective_value(model), model,timer;\n",
    "end"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "781e82d0-ba45-4095-9c89-9c426476658d",
   "metadata": {},
   "source": [
    "## Formulation par hôpital\n",
    "\n",
    "Pour le modèle par hôpital, on commence par définir une fonction qui construit et affiche les cycles solution de cette formulation."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "b0479c3e-b7f5-4039-94b9-c92717a18639",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "compute_solution_K (generic function with 1 method)"
      ]
     },
     "execution_count": 2,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "function compute_solution_K(model,n)\n",
    "    \"\"\"\n",
    "    Fonction qui construit et affiche les cycles solutions de la formulation par hôpital.\n",
    "    \n",
    "    Entrées : \n",
    "        - model : JuMP Model - Modèle résolu du problème\n",
    "        - n     : Integer    - Nombre de sommets dans le graphe\n",
    "    \"\"\"\n",
    "\n",
    "    # Récupération des valeurs optimales des variables et de la valeur optimale\n",
    "    X = value.(model[:x])\n",
    "    \n",
    "    # Récupération des arcs solution\n",
    "    arcs = [[] for i in 1:n]\n",
    "    for (i,j,k) in axes(X)[1]\n",
    "        if abs(X[(i,j,k)]-1) < 1e-6\n",
    "            push!(arcs[k],(i,j))\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    cycles = filter(x -> !isempty(x), arcs)\n",
    "\n",
    "    println(\"Cycles :\")\n",
    "    # Tant qu'il reste des arcs à traiter\n",
    "    for c in range(1,length(cycles))\n",
    "        arcs = cycles[c]\n",
    "        next = arcs[1]\n",
    "        cycle = []\n",
    "        \n",
    "        while(length(arcs) > 0)\n",
    "            # On ajoute l'arc au cycle courant\n",
    "            push!(cycle,next)\n",
    "            # On retire l'arc ajouté de la liste \"arcs\"\n",
    "            arcs = filter(t -> t != next,arcs)\n",
    "\n",
    "            # On met à jour l'arc suivant\n",
    "            next = filter(t -> next[2] in t, arcs)\n",
    "            \n",
    "            if length(next) > 0\n",
    "                # Cas où le cycle courant n'est pas terminé\n",
    "                next = next[1]\n",
    "            else\n",
    "                # Cas où le cycle courant est terminé\n",
    "                ## On l'ajoute au vecteur des cycles solution\n",
    "                push!(cycles,cycle)\n",
    "                print(\"  - \")\n",
    "                println(cycle)\n",
    "            end\n",
    "        end\n",
    "    end \n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "cdc89c16",
   "metadata": {
    "scrolled": true
   },
   "outputs": [],
   "source": [
    "function compact_cycle_K(graph,K,logs)\n",
    "    \"\"\"\n",
    "    Crée le modèle de transplantations en dominos par hôpital.\n",
    "\n",
    "    Entrées : \n",
    "        - graph : Array   - Vecteur contenant les propriétés du graphe (n,A,W,preds,succ)\n",
    "        - K     : Integer - Taille maximale des cycles de transplantation\n",
    "        - logs  : Boolean - Booléen pour l'affichage de la solution\n",
    "    \n",
    "    Sortie : \n",
    "        - objective_value(model) : Float      - Valeur optimale du modèle résolu \n",
    "        - model                  : JuMP Model - Modèle du problème\n",
    "        - timer                  : Float      - Temps de calcul\n",
    "    \"\"\"\n",
    "    # Récupération des propriétés du graphe\n",
    "    n = graph[1]\n",
    "    A = graph[2]\n",
    "    W = graph[3]\n",
    "    preds = graph[4]\n",
    "    succ = graph[5]\n",
    "    \n",
    "    # Vecteur des sommets et vecteur des hôpitaux\n",
    "    nb_max = Integer(floor(n/2))\n",
    "    H = Array(1:nb_max)\n",
    "    V = Array(1:n)\n",
    "    \n",
    "    # Variables\n",
    "    ## On crée un ensemble tri-dimensionnel (i,j,k), pour tout arc (i,j) et hôpital k\n",
    "    triplets=[]\n",
    "    for a in A\n",
    "        for k in H\n",
    "            push!(triplets,(a[1],a[2],k))\n",
    "        end\n",
    "    end \n",
    "        \n",
    "    # Création du modèle\n",
    "    model = Model(HiGHS.Optimizer); \n",
    "    set_optimizer_attribute(model,\"output_flag\",false)\n",
    "\n",
    "    # Variables\n",
    "    ## x[i,j,k]=1 la transplantation i->j est faite dans l'hôpital k\n",
    "    @variable(model, x[triplets], Bin);\n",
    "    \n",
    "    # Fonction objectif: On souhaite maximiser la somme des coûts des transplantations\n",
    "    @objective(model, Max, sum(x[a]*W[a[1],a[2]] for a in triplets));\n",
    "    \n",
    "    # Contraintes\n",
    "    ## Un couple peut donner, au plus, à un autre couple\n",
    "    @constraint(model,c1[i in V],sum(x[(i,j,k)] for j in succ[i] for k in H)<=1)\n",
    "    ## Si un couple donne dans un hôpital k, alors il doit recevoir dans ce même hôpital\n",
    "    @constraint(model,c2[i in V, k in H],sum(x[(i,j,k)] for j in succ[i])==sum(x[(j,i,k)] for j in preds[i]))\n",
    "    ## Un hôpital peut faire, au plus, K transplantations simultanées\n",
    "    @constraint(model,c3[k in H],sum(x[(i,j,k)] for (i,j) in A)<=K)\n",
    "        \n",
    "    \n",
    "    timer = @timed optimize!(model);  \n",
    "    # Affichage de la solution si souhaité\n",
    "    if logs \n",
    "        println(\"***** Hôpital *****\");\n",
    "        # Statistiques de la solution\n",
    "        println(\"Statut de la solution = \", termination_status(model));\n",
    "        println(\"Valeur optimale = \", objective_value(model));\n",
    "        println(\"Temps = \", timer[2])\n",
    "    end\n",
    "            \n",
    "    return objective_value(model),model,timer;\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "608561e6-c631-4601-b8e0-406f5b84b54e",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Fonctions importées avec succès\n"
     ]
    }
   ],
   "source": [
    "println(\"Formulations compactes importées avec succès.\")"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.9.0",
   "language": "julia",
   "name": "julia-1.9"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.9.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
