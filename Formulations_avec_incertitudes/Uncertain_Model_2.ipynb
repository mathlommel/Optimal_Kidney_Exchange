{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "86c568f5",
   "metadata": {},
   "source": [
    "# Formulation avec cycles de taille 2"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "bc48a500",
   "metadata": {},
   "outputs": [],
   "source": [
    "function get_cycles_2(graph)\n",
    "    \"\"\"\n",
    "    Construit les données utiles du graphe restreint aux cycles de taille 2.\n",
    "    \n",
    "    Entrée : \n",
    "        - graph : array - Liste des caractéristiques du graphe considéré\n",
    "    \n",
    "    Sortie :\n",
    "        - C_2          : array - Liste des identifiants des cycles de taille 2\n",
    "        - collect(V_2) : array - Liste des sommets concernés par les cycles de taille 2\n",
    "        - C_v          : array - Liste des identifiants des cycles passant par chaque sommet \n",
    "        - A_2          : array - Liste des arcs concernés par les cycles de taille 2\n",
    "    \"\"\"\n",
    "    # Récupération des données du graphe\n",
    "    n = graph[1]\n",
    "    A = graph[2]\n",
    "    preds = graph[4]\n",
    "    succ = graph[5]\n",
    "    \n",
    "    # Initialisation des listes de cycles, sommets, et cycles passant par chaque sommet  \n",
    "    C_2 = []                          # chaque cycle est représenté par un des deux arcs qui le compose\n",
    "    V_2 = Set()                       # liste des sommets du graphe restreint aux cycles de taille 2\n",
    "    C_v = [[] for i in Array(1:n)]    # liste des cycles passant par chaque sommet du graphe\n",
    "    A_2 = []                          # liste des arcs du graphe restreint aux cycles de taille 2\n",
    "    \n",
    "    # Mise a jour itérative des listes\n",
    "    for i in Array(1:n)\n",
    "        for j in succ[i]\n",
    "            # On ne considère que la moitié des arcs\n",
    "            if i > j\n",
    "                continue\n",
    "            end\n",
    "            # Si on a un cycle possible\n",
    "            if (j,i) in A \n",
    "                # Ajout du cycle dans la liste C_2\n",
    "                push!(C_2,(i,j))\n",
    "                    \n",
    "                push!(A_2,(i,j))\n",
    "                push!(A_2,(j,i))\n",
    "                        \n",
    "                # Ajout du cycle dans les ensembles C_v[i] et C_v[j]\n",
    "                push!(C_v[i], (i,j))\n",
    "                push!(C_v[j], (i,j))\n",
    "                        \n",
    "                # Ajout des sommets dans la liste des sommets \"intéressants\"\n",
    "                push!(V_2, i)\n",
    "                push!(V_2, j)\n",
    "                \n",
    "            end  \n",
    "        end\n",
    "    end\n",
    "        \n",
    "    return C_2,collect(V_2),C_v, A_2\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "28cef1dc",
   "metadata": {},
   "outputs": [
    {
     "ename": "LoadError",
     "evalue": "LoadError: UndefVarError: `@variable` not defined\nin expression starting at In[1]:34",
     "output_type": "error",
     "traceback": [
      "LoadError: UndefVarError: `@variable` not defined\nin expression starting at In[1]:34",
      ""
     ]
    }
   ],
   "source": [
    "function uncertain_model_2(C_2,V_2,C_v, S, B, logs, relax=false, t_max=120)\n",
    "    \"\"\"\n",
    "    Crée et optimise le programme linéaire avec cycles de taille 2.\n",
    "\n",
    "    Entrées : \n",
    "        - C_2   : array   - Liste des identifiants des cycles de taille 2\n",
    "        - V_2   : array   - Liste des sommets concernés par les cycles de taille 2\n",
    "        - C_v   : array   - Liste des identifiants des cycles passant par chaque sommet \n",
    "        - S     : array   - Liste des scénarios\n",
    "        - B     : Integer - Budget (nombre de tests maximum)\n",
    "        - logs  : Boolean - Booléen pour l'affichage de la solution\n",
    "        - relax : Boolean - Booléen pour relâcher la contrainte d'intégrité\n",
    "        - t_max : Integer - Temps maximum accordé pour la résolution du problème\n",
    "    \n",
    "    Sortie : \n",
    "        - objective_value(model) : Float      - Valeur optimale du modèle résolu \n",
    "        - model                  : JuMP Model - Modèle du problème\n",
    "        - timer                  : Float      - Temps de calcul\n",
    "    \"\"\"\n",
    "    # Vecteur des sommets \n",
    "    N = Array(1:length(S))\n",
    "    \n",
    "    # Création du modèle\n",
    "    model = Model(HiGHS.Optimizer); \n",
    "    set_optimizer_attribute(model,\"output_flag\",false)\n",
    "\n",
    "    # Variables\n",
    "    ## x[(i,j)] = 1 ssi on réalise un test croisé sur les deux arcs (i,j) et (j,i)    \n",
    "    @variable(model, x[C_2], Bin);\n",
    "    ## z[(i,j),s] = 1 ssi le cycle [(i,j),(j,i)] effectue un transfert dans le scénario s\n",
    "    if relax\n",
    "        @variable(model, z[C_2,N] >=0);\n",
    "    else\n",
    "        @variable(model, z[C_2,N], Bin);\n",
    "    end\n",
    "    \n",
    "    # Fonction objectif: On souhaite maximiser la moyenne empirique du nombre de transplantations\n",
    "    @objective(model, Max, 2/length(S)*sum(z[c,s] for c in C_2 for s in N));\n",
    "    \n",
    "    # Contraintes\n",
    "    ## On réalise au plus B tests croisés (B = budget de tests)\n",
    "    @constraint(model,c1, 2*sum(x[a] for a in C_2) <= B)\n",
    "        \n",
    "    ## On ne peut choisir un arc que si le test croisé a été réalisé et a été réussi pour cet arc (dans le scénario s)\n",
    "    @constraint(model,c2[c in C_2, s in N],\n",
    "        2 * z[ (c[1],c[2]) , s ] <= x[c] * ( S[s][c[2],c[1]] + S[s][c[1],c[2]]) )\n",
    "    \n",
    "    ## Chaque sommet peut être parcouru par au plus un cycle, dans le scénario s\n",
    "    @constraint(model,c4[v in V_2, s in N], sum(z[c,s] for c in C_v[v]) <= 1)\n",
    "    \n",
    "    # Ajout du time_limit souhaité\n",
    "    set_time_limit_sec(model, t_max)\n",
    "    \n",
    "    timer = @timed optimize!(model);  \n",
    "    \n",
    "    # Affichage si time_limit atteint\n",
    "    if termination_status(model) == TIME_LIMIT\n",
    "        println(\"Attention : time_limit atteint\")\n",
    "    end\n",
    "    # Affichage de la solution si souhaité\n",
    "    if logs \n",
    "        println(\"***** Uncertain *****\");\n",
    "        # Statistiques de la solution\n",
    "        println(\"Statut de la solution = \", termination_status(model));\n",
    "        println(\"Valeur optimale = \", objective_value(model));\n",
    "        println(\"Temps = \", timer[2])\n",
    "    end\n",
    "            \n",
    "    return objective_value(model),model,timer;\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "da9bd068",
   "metadata": {},
   "outputs": [],
   "source": [
    "function pb_recours_2(C_2,V_2,C_v, S, logs, X, relax=false)\n",
    "    \"\"\"\n",
    "    Résout le programme linéaire avec cycles de taille 2 pour un ensemble de tests \"X\" donné.\n",
    "\n",
    "    Entrées : \n",
    "        - C_2   : array          - Liste des identifiants des cycles de taille 2\n",
    "        - V_2   : array          - Liste des sommets concernés par les cycles de taille 2\n",
    "        - C_v   : array          - Liste des identifiants des cycles passant par chaque sommet \n",
    "        - S     : array          - Liste des scénarios\n",
    "        - logs  : Boolean        - Booléen pour l'affichage de la solution\n",
    "        - X     : DenseAxisArray - Liste des tests croisés à réaliser\n",
    "        - relax : Boolean        - Booléen pour relâcher la contrainte d'intégrité\n",
    "    \n",
    "    Sortie : \n",
    "        - objective_value(model) : Float      - Valeur optimale du modèle résolu \n",
    "        - model                  : JuMP Model - Modèle du problème\n",
    "        - timer                  : Float      - Temps de calcul\n",
    "    \"\"\"\n",
    "    # Vecteur des sommets \n",
    "    N = Array(1:length(S))\n",
    "\n",
    "    # Création du modèle\n",
    "    model = Model(HiGHS.Optimizer); \n",
    "    set_optimizer_attribute(model,\"output_flag\",false)\n",
    "\n",
    "    # Variables\n",
    "    ## z[(i,j),s] = 1 ssi le cycle [(i,j),(j,i)] effectue un transfert dans le scénario s\n",
    "    @variable(model, z[C_2,N], Bin);\n",
    "    \n",
    "    # Fonction objectif: On souhaite maximiser la moyenne empirique du nombre de transplantations\n",
    "    @objective(model, Max, 2/length(S)*sum(z[c,s] for c in C_2 for s in N));\n",
    "    \n",
    "    # Contraintes\n",
    "    ## On ne peut choisir un arc que si le test croisé a été réalisé et a été réussi pour cet arc (dans le scénario s)\n",
    "    @constraint(model,c2[c in C_2, s in N],\n",
    "        2 * z[ (c[1],c[2]) , s ] <= X[c] * ( S[s][c[2],c[1]] + S[s][c[1],c[2]]) )\n",
    "    \n",
    "    ## Chaque sommet peut être parcouru par au plus un cycle, dans le scénario s\n",
    "    @constraint(model,c4[v in V_2, s in N], sum(z[c,s] for c in C_v[v]) <= 1)\n",
    "    \n",
    "    if relax\n",
    "        # Si l'on souhaite résoudre le problème avec contrainte d'intégrité\n",
    "        relax_integrality(model)\n",
    "    end \n",
    "    \n",
    "    timer = @timed optimize!(model);  \n",
    "    # Affichage de la solution si souhaité\n",
    "    if logs \n",
    "        println(\"***** Uncertain *****\");\n",
    "        # Statistiques de la solution\n",
    "        println(\"Statut de la solution = \", termination_status(model));\n",
    "        println(\"Valeur optimale = \", objective_value(model));\n",
    "        println(\"Temps = \", timer[2])\n",
    "    end\n",
    "            \n",
    "    return objective_value(model),model,timer;\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "5e4bbeca",
   "metadata": {},
   "outputs": [],
   "source": [
    "function compute_solution_uncertain_2(model)\n",
    "    \"\"\"\n",
    "    Crée et affiche les cycles solutions rendus par le modèle fourni.\n",
    "    \n",
    "    Entrées : \n",
    "        - model : JuMP Model - Modèle résolu du problème\n",
    "    \n",
    "    Sorties : \n",
    "        - arcs : Array - Cycles solutions\n",
    "    \"\"\"\n",
    "    # Récupération des valeurs optimales des variables et de la valeur optimale\n",
    "    X = value.(model[:x])\n",
    "\n",
    "    # Récupération des arcs solution\n",
    "    arcs = []\n",
    "    for (i,j) in axes(X)[1]\n",
    "        if abs(X[(i,j)]-1) < 1e-6\n",
    "            push!(arcs,(i,j))\n",
    "        end\n",
    "    end\n",
    "        \n",
    "    println(\"Tests croisés à réaliser : \")\n",
    "    test = true\n",
    "    while length(arcs) > 0\n",
    "        (i,j) = arcs[1]\n",
    "        print(\"  - \")\n",
    "        print((i,j)) \n",
    "        print(\" & \")\n",
    "        println((j,i))\n",
    "\n",
    "        arcs = setdiff(arcs, [(i,j),(j,i)])\n",
    "    end\n",
    "    \n",
    "    return arcs\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f9472267",
   "metadata": {},
   "outputs": [],
   "source": [
    "println(\"Fonctions de résolution du problème de taille 2 importées avec succès.\")"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.9.0",
   "language": "julia",
   "name": "julia-1.9"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.9.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
