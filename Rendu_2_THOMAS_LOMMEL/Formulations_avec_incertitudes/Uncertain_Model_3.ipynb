{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "9b1a5a21",
   "metadata": {},
   "source": [
    "# Formulation avec cycles de taille 3"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6be4c158",
   "metadata": {},
   "outputs": [],
   "source": [
    "function get_cycles_3(graph)\n",
    "    \"\"\"\n",
    "    Construit les données utiles du graphe restreint aux cycles de taille 3.\n",
    "    \n",
    "    Entrée : \n",
    "        - graph : array - Liste des caractéristiques du graphe.\n",
    "    \n",
    "    Sortie :\n",
    "        - cycles       : array - Liste des cycles de taille <= 3\n",
    "        - C_a          : array - Liste des identifiants des cycles contenant l'arc a\n",
    "        - C_v          : array - Liste des identifiants des cycles passant par le sommet v\n",
    "        - U            : array - Liste des coûts de chaque cycle\n",
    "        - collect(A_3) : array - Liste des arcs concernés par les cycles\n",
    "        - collect(V_3) : array - Liste des sommets concernés par les cycles\n",
    "    \"\"\"\n",
    "    \n",
    "    # Récupération des propriétés du graphe\n",
    "    n = graph[1]\n",
    "    A = graph[2]\n",
    "    preds = graph[4]\n",
    "    succ = graph[5]\n",
    "    \n",
    "    # Initialisation des listes \n",
    "    cycles = []                           # Liste des cycles de taille <= 3\n",
    "    visited = []                          # Liste des cycles déjà ajoutés dans cycles\n",
    "    C_a = [ [] for i in 1:n, j in 1:n ]   # Liste des cycles contenant l'arc \"(i,j)\" (pour tout arc)\n",
    "    C_v = [ [] for i in 1:n]              # Liste des cycles passant par le sommet \"i\" (pour tout sommet)\n",
    "    U = []                                # Coût du cycle ajouté\n",
    "    A_3 = Set()                           # Ensemble des arcs impliqués dans les cycles de taille <= 3\n",
    "    V_3 = Set()                           # Ensemble des sommets impliqués dans les cycles de taille <= 3\n",
    "    \n",
    "    # On parcourt les sommets du graphe\n",
    "    for (i,j) in A\n",
    "        if i > j \n",
    "            # Pour casser la symétrie\n",
    "            continue\n",
    "        end\n",
    "        # Intersection des successeurs de j et des prédecesseurs de i\n",
    "        sommets = intersect(succ[j],preds[i])\n",
    "        \n",
    "        # Récupération des cycles de taille 3\n",
    "        for s in sommets\n",
    "            # Si le cycle (i,j,s) n'a pas encore été visité\n",
    "            if !(Set((i,s,j)) in visited)\n",
    "                # Ajout du cycle\n",
    "                push!(cycles, [(i,j),(j,s),(s,i)])\n",
    "                \n",
    "                # Mise à jour de A_3\n",
    "                push!(A_3,(i,j))\n",
    "                push!(A_3,(j,s))\n",
    "                push!(A_3,(s,i))\n",
    "                            \n",
    "                # Mise à jour de V_3\n",
    "                push!(V_3,i)\n",
    "                push!(V_3,j)\n",
    "                push!(V_3,s)\n",
    "                \n",
    "                # Coût du cycle\n",
    "                push!(U,3)\n",
    "                \n",
    "                # Mise à jour de visited\n",
    "                push!(visited,Set((i,s,j)))\n",
    "                \n",
    "                # Mises à jour de C_a et C_v\n",
    "                push!(C_a[i,j],length(cycles))\n",
    "                push!(C_a[j,s],length(cycles))\n",
    "                push!(C_a[s,i],length(cycles))\n",
    "                            \n",
    "                push!(C_v[i], length(cycles))\n",
    "                push!(C_v[j], length(cycles))\n",
    "                push!(C_v[s], length(cycles))\n",
    "            end\n",
    "        end\n",
    "        \n",
    "        # Si l'arc (j,i) existe, il existe un cycle de taille 2\n",
    "        if (j,i) in A \n",
    "            # Ajout du cycle dans la liste C_2\n",
    "            push!(cycles,[(i,j),(j,i)])\n",
    "                \n",
    "            # Mises à jour de A_3 et V_3\n",
    "            push!(A_3,(i,j))\n",
    "            push!(A_3,(j,i))\n",
    "                        \n",
    "            push!(V_3,i)\n",
    "            push!(V_3,j)\n",
    "            \n",
    "            # Ajout du coût du cycle\n",
    "            push!(U,2)\n",
    "                \n",
    "            # Mises à jour de C_v et C_a\n",
    "            push!(C_v[i], length(cycles))\n",
    "            push!(C_v[j], length(cycles))\n",
    "\n",
    "            push!(C_a[i,j], length(cycles))\n",
    "            push!(C_a[j,i], length(cycles))\n",
    "        end\n",
    "            \n",
    "    end\n",
    "    \n",
    "    return cycles, C_a, C_v, U, collect(A_3), collect(V_3)\n",
    "    \n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "59d173b0",
   "metadata": {},
   "outputs": [
    {
     "ename": "LoadError",
     "evalue": "LoadError: UndefVarError: `@variable` not defined\nin expression starting at In[1]:36",
     "output_type": "error",
     "traceback": [
      "LoadError: UndefVarError: `@variable` not defined\nin expression starting at In[1]:36",
      ""
     ]
    }
   ],
   "source": [
    "function uncertain_model_3(A_3,V_3,C,C_a,C_v,U, S, B, logs,relax=false,t_max=120)\n",
    "    \"\"\"\n",
    "    Crée et optimise le programme linéaire avec cycles de taille <= 3.\n",
    "\n",
    "    Entrées : \n",
    "        - A_3   : array   - Liste des arcs concernés par les cycles de taille <= 3\n",
    "        - V_3   : array   - Liste des sommets concernés par les cycles de taille <= 3\n",
    "        - C     : array   - Liste des cycles de taille <= 3\n",
    "        - C_a   : array   - Liste des identifiants des cycles de taille <= 3\n",
    "        - C_v   : array   - Liste des identifiants des cycles passant par chaque sommet \n",
    "        - U     : array   - Liste des coûts de chaque cycle\n",
    "        - S     : array   - Liste des scénarios\n",
    "        - B     : Integer - Budget (nombre de tests maximum)\n",
    "        - logs  : Boolean - Booléen pour l'affichage de la solution\n",
    "        - relax : Boolean - Booléen pour relâcher la contrainte d'intégrité\n",
    "        - t_max : Integer - Temps maximum accordé pour la résolution du problème\n",
    "    \n",
    "    Sortie : \n",
    "        - objective_value(model) : Float      - Valeur optimale du modèle résolu \n",
    "        - model                  : JuMP Model - Modèle du problème\n",
    "        - timer                  : Float      - Temps de calcul\n",
    "    \"\"\"\n",
    "    # Vecteur des sommets et vecteur des hôpitaux\n",
    "    N = Array(1:length(S))\n",
    "\n",
    "    # Création du modèle\n",
    "    model = Model(HiGHS.Optimizer); \n",
    "    set_optimizer_attribute(model,\"output_flag\",false)\n",
    "\n",
    "    # Variables\n",
    "    # X[(i,j)] = 1 ssi on réalise le test croisé sur l'arc (i,j)\n",
    "    @variable(model, x[A_3], Bin);\n",
    "    # z[c,s] = 1 ssi on choisit le cycle d'indice c dans le scénario s\n",
    "    if relax\n",
    "        @variable(model, z[1:length(C),N]>=0);\n",
    "    else\n",
    "        @variable(model, z[1:length(C),N], Bin);\n",
    "    end\n",
    "\n",
    "    # Fonction objectif: On souhaite maximiser la moyenne empirique du nombre de transplantation\n",
    "    @objective(model, Max, 1/length(S)*sum(U[c]*z[c,s] for c in 1:length(C) for s in N));\n",
    "    \n",
    "    # Contraintes\n",
    "    ## On réalise au plus B tests croisés (B = budget de tests)\n",
    "    @constraint(model,c1, sum(x[a] for a in A_3) <= B)\n",
    "    \n",
    "    ## On ne peut choisir un arc que si le test croisé a été réalisé et a été réussi pour cet arc\n",
    "    @constraint(model,c2[a in A_3, s in N],\n",
    "        sum(z[c,s] for c in C_a[a[1],a[2]]) <= x[a] * S[s][a[1],a[2]])\n",
    "    \n",
    "    ## Chaque sommet peut être parcouru par au plus un cycle, dans le scénario s\n",
    "    @constraint(model,c4[v in V_3, s in N], sum(z[c,s] for c in C_v[v]) <= 1)\n",
    "    \n",
    "    # Ajout du time_limit donné\n",
    "    set_time_limit_sec(model, t_max)\n",
    "    \n",
    "    # Résolution du problème\n",
    "    timer = @timed optimize!(model);  \n",
    "    \n",
    "    # Affichage si time_limit atteint\n",
    "    if termination_status(model) == TIME_LIMIT\n",
    "        println(\"Attention : time_limit atteint\")\n",
    "    end\n",
    "    # Affichage de la solution si souhaité\n",
    "    if logs \n",
    "        println(\"***** Uncertain *****\");\n",
    "        # Statistiques de la solution\n",
    "        println(\"Statut de la solution = \", termination_status(model));\n",
    "        println(\"Valeur optimale = \", objective_value(model));\n",
    "        println(\"Temps = \", timer[2])\n",
    "    end\n",
    "            \n",
    "    return objective_value(model),model,timer;\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "5481f3c4",
   "metadata": {},
   "outputs": [],
   "source": [
    "function pb_recours_3(A_3,V_3,C, C_a, C_v, U, S, logs, X, relax=false)\n",
    "    \"\"\"\n",
    "    Crée et optimise le programme linéaire avec cycles de taille <= 3.\n",
    "\n",
    "    Entrées : \n",
    "        - A_3   : array          - Liste des arcs concernés par les cycles de taille <= 3\n",
    "        - V_3   : array          - Liste des sommets concernés par les cycles de taille <= 3\n",
    "        - C     : array          - Liste des cycles de taille <= 3\n",
    "        - C_a   : array          - Liste des identifiants des cycles de taille <= 3\n",
    "        - C_v   : array          - Liste des identifiants des cycles passant par chaque sommet \n",
    "        - U     : array          - Liste des coûts de chaque cycle\n",
    "        - S     : array          - Liste des scénarios\n",
    "        - logs  : Boolean        - Booléen pour l'affichage de la solution\n",
    "        - X     : DenseAxisArray - Liste des tests croisés réalisés\n",
    "        - relax : Boolean        - Booléen pour relâcher la contrainte d'intégrité\n",
    "    \n",
    "    Sortie : \n",
    "        - objective_value(model) : Float      - Valeur optimale du modèle résolu \n",
    "        - model                  : JuMP Model - Modèle du problème\n",
    "        - timer                  : Float      - Temps de calcul\n",
    "    \"\"\"\n",
    "   \n",
    "    # Vecteur des sommets et vecteur des hôpitaux\n",
    "    N = Array(1:length(S))\n",
    "\n",
    "    # Création du modèle\n",
    "    model = Model(HiGHS.Optimizer); \n",
    "    set_optimizer_attribute(model,\"output_flag\",false)\n",
    "\n",
    "    # Variables\n",
    "    # z[c,s] = 1 ssi on choisit le cycle d'indice c dans le scénario s\n",
    "    @variable(model, z[1:length(C),N], Bin);\n",
    "    \n",
    "    # Fonction objectif: On souhaite maximiser la moyenne empirique du nombre de transplantation\n",
    "    @objective(model, Max, 1/length(S)*sum(U[c]*z[c,s] for c in 1:length(C) for s in N));\n",
    "    \n",
    "    # Contraintes\n",
    "\n",
    "    ## On ne peut choisir un arc que si le test croisé a été réalisé et a été réussi pour cet arc\n",
    "    @constraint(model,c2[a in A_3, s in N],\n",
    "        sum(z[c,s] for c in C_a[a[1],a[2]]) <= X[a] * S[s][a[1],a[2]])\n",
    "    \n",
    "    ## Chaque sommet peut être parcouru par au plus un cycle, dans le scénario s\n",
    "    @constraint(model,c4[v in V_3, s in N], sum(z[c,s] for c in C_v[v]) <= 1)\n",
    "    \n",
    "    # On relâche l'intégrité si souhaité\n",
    "    if relax\n",
    "        # Si l'on souhaite résoudre le problème avec contrainte d'intégrité\n",
    "        relax_integrality(model)\n",
    "    end \n",
    "    \n",
    "    # Résolution du problème\n",
    "    timer = @timed optimize!(model);  \n",
    "\n",
    "    # Affichage de la solution si souhaité\n",
    "    if logs \n",
    "        println(\"***** Uncertain *****\");\n",
    "        # Statistiques de la solution\n",
    "        println(\"Statut de la solution = \", termination_status(model));\n",
    "        println(\"Valeur optimale = \", objective_value(model));\n",
    "        println(\"Temps = \", timer[2])\n",
    "    end\n",
    "            \n",
    "    return objective_value(model),model,timer;\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "26141afd",
   "metadata": {},
   "outputs": [],
   "source": [
    "function compute_solution_uncertain(model)\n",
    "    \"\"\"\n",
    "    Crée et affiche les cycles solutions rendu par le modèle fourni.\n",
    "    \n",
    "    Entrées : \n",
    "        - model : JuMP Model - Modèle résolu du problème\n",
    "    \n",
    "    Sorties : \n",
    "        - arcs : Array - Cycles solutions\n",
    "    \"\"\"\n",
    "    # Récupération des valeurs optimales des variables et de la valeur optimale\n",
    "    X = value.(model[:x])\n",
    "\n",
    "    # Récupération des arcs solution\n",
    "    arcs = []\n",
    "    for (i,j) in axes(X)[1]\n",
    "        if abs(X[(i,j)]-1) < 1e-6\n",
    "            push!(arcs,(i,j))\n",
    "        end\n",
    "    end\n",
    "        \n",
    "    println(\"Tests croisés à réaliser : \")\n",
    "    for arc in arcs\n",
    "        print(\"  - \")\n",
    "        println(arc) \n",
    "    end\n",
    "    \n",
    "    return arcs\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "79dc712c",
   "metadata": {},
   "outputs": [],
   "source": [
    "println(\"Fonctions de résolution du problème de taille 3 importées avec succès.\")"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.9.0",
   "language": "julia",
   "name": "julia-1.9"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.9.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
